Ns ->
    "the phenomenon"
  | "an object"
  | "the cogitatum" [ " (qua cogitatum)" ]
  | [ "transcendental " ] "phenomenology"
  | ( "pure" [ " and genuine" ] | "scientific" ) " evidence"
  | "cognition"
  | "the ego cogito"
  | "transcendental subjectivity"
  | "the transcendental-phenomenological reduction"
  | "the " [ "phenomenological " ] "epoche"
  | "the Objective world"
  | "a transcendental grounding of " ( <Ns> | <Np> )
  | "philosophy"
  | "the stream of " <Np>
  | "what is itself given"
  | "the repeatable act of grounding" [ " of " <Np> ]
  | "an infinite horizon of approximations"
  | "the " <ego.type> " Ego"
  | "the fundamental form of this universal synthesis"
  | "the " [ "all-embracing " ] "consciousness of internal time"
  | "immanent time"
  | "the task of clarifying " <Np>
  | "the whole of conscious life"
  ;
Np ->
    "noetic acts"
  | "cogitationes"
  | "noematic descriptions"
  | [ "separated " ] "modes of consciousness"
  | "multiplicities of " <Ns>
  | "experiences"
  ;
Vst ->
    "denotes a universal primal phenomenon of"
  | "is only a modality of"
  | "becomes modalized also in correlation with"
  | "stands in contrast to the accidental being \"for me\" of"
  | "is a clarification of the striving for"
  | "becomes adjusted to"
  ;
Vpt ->
    "exist for us thanks to"
  | "denote the universal primary phenomena of"
  | "are only modalities of"
  | "become modalized also in correlation with"
  | "stand in contrast to" [ " the accident being \"for me\" of" ]
  | "become adjusted to"
  ;
Vsi ->
    "inhibits acceptance of " <Ns> " as existent"
  | "is actual"
  | "does not yet produce for us any abiding being"
  | "is \"in itself\""
  | "is an infinite idea, related to infinities of harmoniously combinable " <Np>
  | "is an agreement of " <Ns> " with the judged " <Np>
  | "can be seen as full proof that, in spite of the continual experiencedness of " <Np> ", a non-being of " <Ns> " is conceivable"
  | "needs to be criticized with regard to its validity and range, before it can be used for the purposes of a radical grounding of " <Ns>
  | "is precisely what makes critical decisions about " <Np> " at all possible"
  | "is unified synthetically"
  | "synthetically constitutes all particular " <Np> " that ever become prominent"
  ;
Vpi ->
    "inhibit " [ "my" | "our" ] "acceptance of " ( <Ns> | <Np> ) " as existent"
  | "do not yet produce for " ( "us" | "me" ) " any abiding being" [ " apart from their relation to " ( <Ns> | <Np> ) ]
  | "are \"in themselves\""
  | "are infinite ideas, related to infinities of harmoniously combinable " <Np> [ " and " <Np> ]
  | "can be seen as " [ "full " ] "proof that, in spite of the continual experiencedness of " <Ns> ", a non-being of " ( <Ns> | <Np> ) " is " ( "conceivable" | "possible" )
  | "need to be criticized with regard to their validity and range, before they can be used for " [ "the purposes of " ] "a radical grounding of " ( <Ns> | <Np> )
  | "are precisely what make critical decisions about " ( <Ns> | <Np> ) " at all possible"
  | "are unified synthetically"
  | "synthetically constitute all particular instances of " <Ns> " that ever become prominent"
  ;
intro.clause ->
    "in the natural attitude"
  | "de facto"
  | "owing to the instability and ambiguity of " ( <Ns> | <Np> )
  | "by immersing ourselves in such a striving"
  | "in other words"
  | "by this preliminary work, here roughly indicated rather than done explicitly"
  | "where this is still wanting"
  | "since the form belonging to a systematic order of " ( <Ns> | <Np> ) " is part of this idea"
  | "in the broadest sense"
  | "as we have said"
  | "in the attitude established by transcendental reduction"
  | "in a maximally broad sense"
  | "if we maintain this attitude"
  | "in the broadest Cartesian sense"
  ;
certainty.clause ->
    "it is plain that"
  | "by immersing ourselves meditatively in the general intentions of " ( <Ns> | <Np> ) ", we discover that"
  | "if we follow up this doubt, it becomes manifest that"
  | "by virtue of my free epoche with respect to the being of the experienced world, the momentous fact is that"
  | "yet it must not be overlooked that"
  | "the fact is that"
  | "we can be sure that"
  | "hence it follows without more ado that"
  | ( <Ns> " calls" | <Np> " call" ) " our attention to the fact that"
  | "we see in advance that"
  | "the fact is evident" [ ", even apodictically evident," ] " that"
  | "it becomes evident that"
  ;
throwaway.clause ->
    "in an extremely broad sense"
  | "consequently"
  | "in respect of this evidence"
  | "however"
  | "mediately"
  | "naturally"
  | "even with respect to the evidence in which " ( <Ns> " presents itself" | <Np> " present themselves" )
  | "as a matter of essential necessity"
  | "perchance"
  | "when we let our thoughts hasten in this manner"
  ;
how.clause ->
    "by conscious conversion into the corresponding " <Np>
  | "by virture of a synthesis in which what is meant coincides and agree with " <Ns>
  | "by a freely actualizable return to " ( <Ns> | <Np> )
  | "by reconciling with " ( <Ns> | <Np> )
  | "by orienting " ( <Ns> | <Np> ) " according to accured insights"
  | "by the fundamental nature of " ( <Ns> | <Np> )
  ;
main.clause ->
    <Ns> [ ", " <throwaway.clause> "," ] <Vst> ( <Ns> | <Np> ) [ "; " <subject.clause> ]
  | <Ns> [ " (" <intro.clause> ")" ] <Vsi> [ <how.clause> ]
  | <Np> [ ", " <throwaway.clause> "," ] <Vpt> [ ", " <how.clause> "," ] ( <Ns> | <Np> )
  | <Np> [ " (" <intro.clause> ")" ] <Vpi> [ ", " <how.clause> ] [ "; " <subject.clause> ]
  ;
ego.type ->
    "reflecting"
  | "phenomenological"
  | "transcendental"
  | "worldly"
  ;
subject.clause ->
    ( "I" | "we" ) " now shift the weight of transcendental evidence of " ( <Ns> | <Np> ) [ "(" <intro.clause> ") " ] "from the " [ <ego.type> ] "ego to " ( <Ns> | <Np> )
  | "we must not let ourselves be frightened by considerations of " <Ns> " and " <Np>
  | "only in reflection do we \"direct\" ourselves to " <Ns> " and to its perceptual directedness to " <Np>
  | "we have not simply lost " <Ns> " for phenomenology; we retain it" [ ", " <throwaway.clause> "," ] <how.clause>
  | "I have the reflection that " <Ns> " is given continuously as an objective unity in a multi-form and changeable multiplicity of " <Np> ", which belong determinately to it"
  | "I" [ ", the meditating phenomenologist," ] " set myself the all-embracing task of uncovering " ( "myself" | <Ns> | <Np> ) <how.clause>
  ;
sentence ->
    [ <certainty.clause> ] <main.clause> "."
  | [ <certainty.clause> ] <main.clause> "."
  | [ <certainty.clause> [ ", " <how.clause> "," ] ] <subject.clause> "."
  ;
paragraph ->
    [ <sentence> ] [ <sentence> ] [ <sentence> ] [ <sentence> ] <sentence> <sentence> <sentence> <sentence> <sentence>
  | [ <sentence> ] [ <sentence> ] <sentence> <sentence> <sentence> <sentence> <sentence>
  | [ <sentence> ] [ <sentence> ] [ <sentence> ] [ <sentence> ] <sentence> <sentence> <sentence> <sentence>
  ;
section ->
  <paragraph> <paragraph> <paragraph> <paragraph> [ <paragraph> ] [ <paragraph> ] [ <paragraph> ] [ <paragraph> ] [ <paragraph> ] [ <paragraph> ]
  ;
